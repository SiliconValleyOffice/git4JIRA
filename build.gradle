buildscript {
    ext.kotlin_version = "1.3.20"
    ext.tornadofx_version = "1.7.17"
    ext.controlsfx_version = "9.0.0"
    ext.sqlite_jdbc_version = "3.25.2"
    ext.rxkotlin_jdbc_version = "0.4.1"
    ext.slf4j_simple_version = "1.8.0-beta2"
    ext.rxkotlinfx_version = "2.2.2"
    ext.rxkotlin_version = "2.3.0"
    ext.junit_jupiter_version = "5.3.1"
    ext.egit_version = "2.1.5"
    ext.gson_version = "2.8.5"
    ext.retrofit_version = "2.5.0"
    ext.moshi_version = "1.8.0"
    ext.okhttp_version = "3.6.0"

    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "net.ltgt.apt" version "0.20"
}

apply plugin: "kotlin"
apply plugin: "kotlin-kapt"
apply plugin: "idea"
apply plugin: "application"

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/source/kapt/main"
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    //Dagger
    compile "com.google.dagger:dagger:2.17"
    kapt 'com.google.dagger:dagger-compiler:2.17'
    
    //Moshi
    implementation("com.squareup.moshi:moshi-kotlin:$moshi_version")
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    //TornadoFX
    compile "no.tornado:tornadofx:$tornadofx_version"

    //ControlsFX
    compile "org.controlsfx:controlsfx:$controlsfx_version"

    //E-GIT
    compile "org.eclipse.mylyn.github:org.eclipse.egit.github.core:$egit_version"

    //sqlite and RxJava-JDBC dependencies
    compile "org.xerial:sqlite-jdbc:$sqlite_jdbc_version"
    compile "org.nield:rxkotlin-jdbc:$rxkotlin_jdbc_version"
    compile "org.slf4j:slf4j-simple:$slf4j_simple_version"

    //Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    //RxKotlin and RxKotlinFX dependencies
    compile "com.github.thomasnield:rxkotlinfx:$rxkotlinfx_version"
    compile "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"

    testCompile "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
}

mainClassName = "com.siliconvalleyoffice.git4jira.Git4JiraApp"

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(" "),
                "Main-Class": mainClassName
        )
    }
    from(configurations.compile.collect { entry -> zipTree(entry) }) {
        exclude "META-INF/MANIFEST.MF"
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}
